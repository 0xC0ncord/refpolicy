## <summary>Red Hat utility to change /etc/fstab.</summary>

########################################
## <summary>
##	Execute updfstab in the updfstab domain.
## </summary>
## <param name="domain">
##	The type of the process performing this action.
## </param>
#
interface(`updfstab_domtrans',`
	gen_require(`
		type updfstab_t, updfstab_exec_t;
	')

	files_search_usr($1)
	corecmd_search_sbin($1)
	domain_auto_trans($1,updfstab_exec_t,updfstab_t)

	allow $1 updfstab_t:fd use;
	allow updfstab_t $1:fd use;
	allow updfstab_t $1:fifo_file rw_file_perms;
	allow updfstab_t $1:process sigchld;
')

########################################
## <summary>
##	Send and receive messages from
##	updfstab over dbus.
## </summary>
## <param name="domain">
##	Domain allowed access.
## </param>
#
interface(`updfstab_dbus_chat',`
	gen_require(`
		type updfstab_t;
		class dbus send_msg;
	')

	allow $1 updfstab_t:dbus send_msg;
	allow updfstab_t $1:dbus send_msg;
')
