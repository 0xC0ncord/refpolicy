## <module name="dmesg" layer="admin">
## <summary>Policy for dmesg.</summary>

########################################
## <interface name="dmesg_domtrans">
##	<description>
##		Execute dmesg in the dmesg domain.
##	</description>
##	<parameter name="domain">
##		The type of the process performing this action.
##	</parameter>
## </interface>
#
define(`dmesg_domtrans',`
	gen_require(`$0'_depend)

	allow $1 dmesg_exec_t:file rx_file_perms;
	allow $1 dmesg_t:process transition;
	type_transition $1 dmesg_exec_t:process dmesg_t;
	dontaudit $1 dmesg_t:process { noatsecure siginh rlimitinh };

	allow $1 dmesg_t:fd use;
	allow dmesg_t $1:fd use;
	allow dmesg_t $1:fifo_file rw_file_perms;
	allow dmesg_t $1:process sigchld;
')

define(`dmesg_domtrans_depend',`
	type dmesg_t, dmesg_exec_t;

	class file rx_file_perms;
	class process { transition noatsecure siginh rlimitinh sigchld };
	class fd use;
	class fifo_file rw_file_perms;
')

########################################
## <interface name="dmesg_exec">
##	<description>
##		Execute dmesg in the caller domain.
##	</description>
##	<parameter name="domain">
##		The type of the process performing this action.
##	</parameter>
## </interface>
#
define(`dmesg_exec',`
	gen_require(`$0'_depend)

	can_exec($1,dmesg_exec_t)

')

define(`dmesg_exec_depend',`
	type dmesg_exec_t;

	class file { getattr read execute execute_no_trans };
')

## </module>
