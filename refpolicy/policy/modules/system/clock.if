## <summary>Policy for reading and setting the hardware clock.</summary>

########################################
## <desc>
##	Execute hwclock in the clock domain.
## </desc>
## <param name="domain">
##	The type of the process performing this action.
## </param>
#
interface(`clock_domtrans',`
	gen_require(`
		type hwclock_t, hwclock_exec_t;
		class fd use;
		class fifo_file rw_file_perms;
	')

	domain_auto_trans($1,hwclock_exec_t,hwclock_t)

	allow $1 hwclock_t:fd use;
	allow hwclock_t $1:fd use;
	allow hwclock_t $1:fifo_file rw_file_perms;
	allow hwclock_t $1:process sigchld;
')

########################################
## <desc>
##	Execute hwclock in the clock domain, and
##	allow the specified role the hwclock domain.
## </desc>
## <param name="domain">
##	The type of the process performing this action.
## </param>
## <param name="role">
##	The role to be allowed the clock domain.
## </param>
## <param name="terminal">
##	The type of the terminal allow the clock domain to use.
## </param>
#
interface(`clock_run',`
	gen_require(`
		type hwclock_t;
		class chr_file { getattr read write ioctl };
	')

	clock_domtrans($1)
	role $2 types hwclock_t;
	allow hwclock_t $3:chr_file { getattr read write ioctl };
')

########################################
##     <desc>
##             Execute hwclock
##     </desc>
##     <param name="domain">
##             The type of the process performing this action.
##     </param>
#
interface(`clock_exec',`
	gen_require(`
		type hwclock_exec_t;
	')

	can_exec($1,hwclock_exec_t)
')

########################################
##     <desc>
##             Allow executing domain to modify clock drift
##     </desc>
##     <param name="domain">
##             The type of the process performing this action.
##     </param>
#
interface(`clock_rw_adjtime',`
	gen_require(`
		type adjtime_t;
		class file rw_file_perms;
	')

	allow $1 adjtime_t:file rw_file_perms;
	files_list_etc($1)
')

