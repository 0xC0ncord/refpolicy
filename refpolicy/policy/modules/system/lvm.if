## <module name="lvm">
## <summary>Policy for logical volume management programs.</summary>

########################################
## <interface name="lvm_domtrans">
##	<description>
##		Execute lvm programs in the lvm domain.
##	</description>
##	<parameter name="domain">
##		The type of the process performing this action.
##	</parameter>
## </interface>
#
interface(`lvm_domtrans',`
	gen_require(`
		type lvm_t, lvm_exec_t;
		class process sigchld;
		class fd use;
		class fifo_file rw_file_perms;
	')

	corecmd_search_sbin($1)
	domain_auto_trans($1, lvm_exec_t, lvm_t)

	allow $1 lvm_t:fd use;
	allow lvm_t $1:fd use;
	allow lvm_t $1:fifo_file rw_file_perms;
	allow lvm_t $1:process sigchld;
')

########################################
## <interface name="lvm_run">
##	<description>
##		Execute lvm programs in the lvm domain.
##	</description>
##	<parameter name="domain">
##		The type of the process performing this action.
##	</parameter>
##	<parameter name="role">
##		The role to allow the LVM domain.
##	</parameter>
##	<parameter name="terminal">
##		The type of the terminal allow the LVM domain to use.
##	</parameter>
## </interface>
#
interface(`lvm_run',`
	gen_require(`
		type lvm_t;
		class chr_file rw_term_perms;
	')

	lvm_domtrans($1)
	role $2 types lvm_t;
	allow lvm_t $3:chr_file rw_term_perms;
')

########################################
## <interface name="lvm_read_config">
##	<description>
##		Read LVM configuration files.
##	</description>
##	<parameter name="domain">
##		The type of the process performing this action.
##	</parameter>
## </interface>
#
interface(`lvm_read_config',`
	gen_require(`
		type lvm_t, lvm_exec_t;
		class dir r_dir_perms;
		class file r_file_perms;
	')

	files_search_etc($1)
	allow $1 lvm_etc_t:dir r_dir_perms;
	allow $1 lvm_etc_t:file r_file_perms;
')

## </module>
