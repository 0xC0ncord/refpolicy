## <summary>Policy for iptables.</summary>

########################################
## <summary>
##	Execute iptables in the iptables domain.
## </summary>
## <param name="domain">
##	Domain allowed access.
## </param>
#
interface(`iptables_domtrans',`
	gen_require(`
		type iptables_t, iptables_exec_t;
	')

	corecmd_search_sbin($1)
	domain_auto_trans($1,iptables_exec_t,iptables_t)

	allow $1 iptables_t:fd use;
	allow iptables_t $1:fd use;
	allow iptables_t $1:fifo_file rw_file_perms;
	allow iptables_t $1:process sigchld;
')

########################################
## <summary>
##	Execute iptables in the iptables domain, and
##	allow the specified role the iptables domain.
## </summary>
## <param name="domain">
##	Domain allowed access.
## </param>
## <param name="role">
##	The role to be allowed the iptables domain.
## </param>
## <param name="terminal">
##	The type of the terminal allow the iptables domain to use.
## </param>
#
interface(`iptables_run',`
	gen_require(`
		type iptables_t;
	')

	iptables_domtrans($1)
	role $2 types iptables_t;
	allow iptables_t $3:chr_file rw_term_perms;
')

########################################
## <summary>
##	Execute iptables in the caller domain.
## </summary>
## <param name="domain">
##	Domain allowed access.
## </param>
#
interface(`iptables_exec',`
	gen_require(`
		type iptables_exec_t;
	')

	corecmd_search_sbin($1)
	can_exec($1,iptables_exec_t)
')
