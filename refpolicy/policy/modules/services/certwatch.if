## <summary>Digital Certificate Tracking</summary>

########################################
## <summary>
##	Domain transition to certwatch.
## </summary>
## <param name="domain">
##	Domain allowed access.
## </param>
#
interface(`certwatch_domtrans',`
	gen_require(`
		type certwatch_exec_t, certwatch_t;
	')

	files_search_usr($1)
	corecmd_search_sbin($1)
	domain_auto_trans($1,certwatch_exec_t,certwatch_t)

	allow $1 certwatch_t:fd use;
	allow certwatch_t $1:fd use;
	allow certwatch_t $1:fifo_file rw_file_perms;
	allow certwatch_t $1:process sigchld;
')

########################################
## <summary>
##	Execute certwatch in the certwatch domain, and
##	allow the specified role the certwatch domain,
##	and use the caller's terminal. Has a sigchld
##	backchannel.
## </summary>
## <param name="domain">
##	The type of the process performing this action.
## </param>
## <param name="role">
##	The role to be allowed the certwatch domain.
## </param>
## <param name="terminal">
##	The type of the terminal allow the certwatch domain to use.
## </param>
#
interface(`certwatach_run',`
	gen_require(`
		type certwatch_t;
	')

	certwatch_domtrans($1)
	role $2 types certwatch_t;
	allow certwatch_t $3:chr_file rw_term_perms;
')
